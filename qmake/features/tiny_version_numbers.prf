# Find version numbers in the version header file and assign them to the
# <TARGET>_VERSION_<MAJOR,MINOR,PATCH,TWEAK> and also to the VERSION qmake variables.
defineTest(tiny_version_numbers) {
    defined(1, var): \
    !isEmpty(1): \
        versionHeader = $$1

    # version.hpp for the TinyORM library project
    else:contains(_PRO_FILE_, ".*\/src\/src\.pro$"): \
        versionHeader = $$absolute_path("../include/orm/version.hpp")

    # version.hpp for the tom example project
    else:contains(_PRO_FILE_, ".*\/examples\/tom\/tom\.pro$"): \
        versionHeader = $$absolute_path("../tom/include/tom/version.hpp")

    # Try to find in the HEADERS
    else: \
        versionHeader = $$find(HEADERS, "(?:.*\/)?version\.h(?:pp)?$")

    versionHeader = $$clean_path($$absolute_path($$versionHeader))

    isEmpty(versionHeader)| \
    !exists($$versionHeader): \
        error( "HEADERS does not contain a version header file 'version.hpp', needed\
                in the tiny_version_numbers.prf." )

    versionFileContent = $$cat($$quote($$versionHeader), lines)
    versionTokens = MAJOR MINOR BUGFIX BUILD STATUS
    regexpTokens = $$join(versionTokens, '|')
    regexp = "_($${regexpTokens}) +([0-9]+|\"-{1}\w+\")"
    versionLines = $$find(versionFileContent, $$regexp)
    versionList =
    hasStatus = false

    isEmpty(versionFileContent)| \
    isEmpty(versionLines): \
        error( "Parse of the '$$versionHeader' version file failed." )

    for(versionLine, versionLines) {
        splittedLine  = $$split(versionLine, ' ')
        versionNumber = $$take_last(splittedLine)
        versionNumber = $$replace(versionNumber, ' ', '')

        for(versionToken, versionTokens) {
            contains(versionLine, ".+_$${versionToken} .+") {
                equals(versionToken, "STATUS") {
                    versionNumber = $$replace(versionNumber, '"', '')
                    hasStatus = true
                }
                versionList += $$versionNumber

                $${TARGET}_VERSION_$${versionToken} = $$versionNumber
                export($${TARGET}_VERSION_$${versionToken})
            }
        }
    }

    # Obtain version status
    $$hasStatus: \
        versionStatus = $$take_last(versionList)

    # Use 3 numbers version on other platforms
    !win32: \
        $$take_last(versionList)

    VERSION = $$join(versionList, '.')$$versionStatus

    export(VERSION)
}
