# Mark all Qt headers as system headers

!win32-clang-msvc:!mingw:!unix: \
    return()

win32-clang-msvc: \
    optionToken = -imsvc
else: \
    optionToken = -isystem

# On MSYS2 with Qt6 are includes in the C:/msys64/ucrt64/include/qt6/ so tagging them
# as -isystem is needed.
# On Unix and also Clang-cl with MSVC it is needed to avoid warnings from system headers.
QMAKE_CXXFLAGS += $$optionToken $$shell_quote($$[QT_INSTALL_HEADERS]/)

for(module, QT) {
    equals(module, "testlib"): \
        QMAKE_CXXFLAGS += $$optionToken $$shell_quote($$[QT_INSTALL_HEADERS]/QtTest/)
    else {
        # Capitalize a first letter, result: -isystem <path>/include/QtCore/
        moduleList = $$split(module, )
        QMAKE_CXXFLAGS += \
            $$optionToken $$shell_quote($$[QT_INSTALL_HEADERS]/Qt$$upper(\
                          $$take_first(moduleList))$$join(moduleList, )/)
    }
}

unset(optionToken)
unset(moduleList)
