# Get the TINY_VCPKG_ROOT, take into account also the VCPKG_ROOT environment variable.
defineReplace(tiny_vcpkg_root) {
    # Defined by a user using the qmake variable
    vcpkgRoot = $$TINY_VCPKG_ROOT

    !isEmpty(vcpkgRoot): \
        return($$clean_path($$vcpkgRoot))

    # Defined by a user using the environment variable
    vcpkgRoot = $$(TINY_VCPKG_ROOT)

    !isEmpty(vcpkgRoot): \
        return($$clean_path($$vcpkgRoot))

    # Use the VCPKG_ROOT environment variable
    vcpkgRoot = $$(VCPKG_ROOT)

    !isEmpty(vcpkgRoot): \
        return($$clean_path($$vcpkgRoot))

    return()
}

# Get the TINY_VCPKG_TRIPLET, if not defined then try to guess the vcpkg triplet
# on the base of the current compiler and OS (based on the QMAKESPEC).
defineReplace(tiny_vcpkg_triplet) {
    # Defined by a user using the qmake variable
    vcpkgTriplet = $$TINY_VCPKG_TRIPLET

    !isEmpty(vcpkgTriplet): \
        return($$vcpkgTriplet)

    # Defined by a user using the environment variable
    vcpkgTriplet = $$(TINY_VCPKG_TRIPLET)

    !isEmpty(vcpkgTriplet): \
        return($$vcpkgTriplet)

    # Try to guess
    win32-msvc|win32-clang-msvc: \
        return(x64-windows)

    # MinGW
    win32-g++|win32-clang-g++: \
        return(x64-mingw-dynamic)

    unix:!macx: \
        return(x64-linux)

    # Use the VCPKG_DEFAULT_TRIPLET environment variable
    vcpkgTriplet = $$(VCPKG_DEFAULT_TRIPLET)

    !isEmpty(vcpkgTriplet): \
        return($$vcpkgTriplet)

    return()
}

# Prepare the TINY_VCPKG_ROOT qmake variable with validation.
defineReplace(tiny_prepare_vcpkg_root) {
    # Prepare the vcpkg root path
    vcpkgRoot = $$1

    isEmpty(vcpkgRoot) | \
    !exists($$vcpkgRoot): \
        vcpkgRoot = $$tiny_vcpkg_root()

    else: \
        vcpkgRoot = $$clean_path($$vcpkgRoot)

    isEmpty(vcpkgRoot): \
        error( "Can't detect the vcpkg root folder, please set the TINY_VCPKG_ROOT\
                qmake or environment variable or set the VCPKG_ROOT environment\
                variable." )

    !exists($$vcpkgRoot): \
        error( "The vcpkg root folder '$${vcpkgRoot}' doesn't exist." )

    return($$vcpkgRoot)
}

# Prepare the TINY_VCPKG_TRIPLET qmake variable with validation.
defineReplace(tiny_prepare_vcpkg_triplet) {
    vcpkgTriplet = $$2

    isEmpty(vcpkgTriplet): \
        vcpkgTriplet = $$tiny_vcpkg_triplet()

    else: \
        vcpkgTriplet = $$vcpkgTriplet

    isEmpty(vcpkgTriplet): \
        error( "Can't detect the vcpkg triplet, please set the TINY_VCPKG_TRIPLET\
                qmake or environment variable or set the VCPKG_DEFAULT_TRIPLET\
                environment variable." )

    return($$vcpkgTriplet)
}

# Prepare the TINY_VCPKG_INCLUDE qmake variable with validation.
defineReplace(tiny_prepare_vcpkg_include_path) {
    vcpkgRoot = $$1
    vcpkgTriplet = $$2

    # Prepare the absolute vcpkg include path
    vcpkgIncludePath = $$quote($${vcpkgRoot}/installed/$${vcpkgTriplet}/include)

    !exists($$vcpkgIncludePath): \
        error( "The vcpkg include/ folder '$${vcpkgIncludePath}' doesn't exist." )

    return($$vcpkgIncludePath)
}
