# Target the g++, clang++, msvc, and clang-cl with msvc compilers
!win32|!msvc|mingw: return()

# https://github.com/ccache/ccache/issues/1040
for(cfFlags, \
    $$list(QMAKE_CFLAGS QMAKE_CXXFLAGS \
           QMAKE_CFLAGS_DEBUG QMAKE_CXXFLAGS_DEBUG \
           QMAKE_CFLAGS_RELEASE_WITH_DEBUGINFO QMAKE_CXXFLAGS_RELEASE_WITH_DEBUGINFO \
           # The Release build type shouldn't contain this flag but replace it anyway
           QMAKE_CFLAGS_RELEASE QMAKE_CXXFLAGS_RELEASE) \
): \
    !isEmpty($$cfFlags): \
        # Replace -Zi and -ZI with the -Z7
        # The ~= doesn't work with \/ character
        $$cfFlags = $$replace($$cfFlags, "(?:-|\/)(?:Zi|ZI)", -Z7)

load(private/tiny_ccache_version)

# Determine whether to disable PCH based on the ccache --print-version
tiny_should_disable_precompile_headers(): \
    CONFIG -= precompile_header

for(tool, $$list(QMAKE_CC QMAKE_CXX QMAKE_LINK QMAKE_LINK_SHLIB QMAKE_LINK_C)): \
    $$tool = ccache $$eval($$tool)
